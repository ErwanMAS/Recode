# Configure template for GNU recode.
# Copyright (C) 1994-1999, 2000, 2001 Free Software Foundation, Inc.
# Process this file with autoconf to produce a configure script.

AC_INIT(src/recode.c)
AC_PREREQ(2.13)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(recode, 3.7)
AT_CONFIG(../src)
ALL_LINGUAS="da de es fr gl it nl pl pt ru sl sv"

AC_PROG_CC
AC_AIX
AC_MINIX
AM_PROG_LIBTOOL

AC_ISC_POSIX
AM_C_PROTOTYPES
AC_C_CONST
AC_C_INLINE
ad_AC_PROG_FLEX
AC_C_BIGENDIAN

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h locale.h memory.h stdbool.h stddef.h stdlib.h string.h unistd.h utime.h)
AC_TYPE_SIGNAL
jm_STRUCT_UTIMBUF
AC_MBSTATE_T

AC_CHECK_FUNCS(dup2 mbrtowc rename setlocale strerror strndup wcrtomb)
AC_REPLACE_FUNCS(strtol strtoul)
AC_FUNC_ALLOCA
AC_FUNC_VPRINTF

jm_FUNC_MALLOC
jm_FUNC_REALLOC
AC_SUBST(LIBOBJS)

AC_CANONICAL_HOST
AM_LANGINFO_CODESET
jm_GLIBC21

AM_WITH_DMALLOC
AM_GNU_GETTEXT

# This is necessary so that .o files in LIBOBJS are also built via
# the ANSI2KNR-filtering rules.
LIBOBJS=`echo $LIBOBJS | sed 's/\.o /\$U.o /g;s/\.o$/\$U.o/'`

LTALLOCA=`echo $ALLOCA | sed 's/\.o/.lo/g'`
AC_SUBST(LTALLOCA)
LTLIBOBJS=`echo $LIBOBJS | sed 's/\.o /.lo /g;s/\.o$/.lo/'`
AC_SUBST(LTLIBOBJS)

AC_OUTPUT([Makefile m4/Makefile doc/Makefile intl/Makefile lib/Makefile
libiconv/Makefile src/Makefile po/Makefile.in tests/Makefile tests/atconfig 
contrib/Makefile])
