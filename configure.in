# Configure template for GNU recode.
# Copyright (C) 1994, 96, 97, 98, 99, 00 Free Software Foundation, Inc.
# Process this file with autoconf to produce a configure script.

AC_INIT(src/recode.c)
AC_PREREQ(2.12)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(recode, 3.5e)
AT_CONFIG(../src)
ALL_LINGUAS="da de el es fr gl it nl pl pt sl sv"

AC_PROG_CC
AC_AIX
AC_MINIX
AM_PROG_LIBTOOL

AC_ISC_POSIX
AM_C_PROTOTYPES
AC_C_CONST
AC_C_INLINE
ad_AC_PROG_FLEX

AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h string.h stdbool.h unistd.h utime.h)
AC_TYPE_SIGNAL
jm_STRUCT_UTIMBUF

AC_CHECK_FUNCS(dup2 rename strerror)
AC_REPLACE_FUNCS(strtol strtoul)
AC_FUNC_ALLOCA
AC_FUNC_VPRINTF

jm_FUNC_MALLOC
jm_FUNC_REALLOC
AC_SUBST(LIBOBJS)

fp_OS_MICROSOFT
AC_MSG_CHECKING(the default charset)
if test -n "${DEFAULT_CHARSET+set}"; then
  AC_MSG_RESULT($DEFAULT_CHARSET)
  AC_DEFINE_UNQUOTED(DEFAULT_CHARSET, "$DEFAULT_CHARSET")
elif test "$fp_cv_os_microsoft" = no; then
  AC_MSG_RESULT(Latin-1)
  AC_DEFINE(DEFAULT_CHARSET, "Latin-1")
else
  AC_MSG_RESULT(IBM-PC)
  AC_DEFINE(DEFAULT_CHARSET, "IBM-PC")
fi

AM_WITH_DMALLOC
fp_WITH_GETTEXT

# This is necessary so that .o files in LIBOBJS are also built via
# the ANSI2KNR-filtering rules.
LIBOBJS=`echo $LIBOBJS | sed 's/\.o /\$U.o /g;s/\.o$/\$U.o/'`

LTALLOCA=`echo $ALLOCA | sed 's/\.o/.lo/g'`
AC_SUBST(LTALLOCA)
LTLIBOBJS=`echo $LIBOBJS | sed 's/\.o /.lo /g;s/\.o$/.lo/'`
AC_SUBST(LTLIBOBJS)

AC_OUTPUT(Makefile contrib/Makefile doc/Makefile i18n/Makefile lib/Makefile
libiconv/Makefile m4/Makefile src/Makefile tests/Makefile tests/atconfig)
